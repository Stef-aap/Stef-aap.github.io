
-- --------------------------------------------------------------------
-- procedure Vref_init (
--   bit in high_low_range = true ,
--   bit in output_enabled = false ) is
-- --------------------------------------------------------------------
-- procedure Vref_off is
-- --------------------------------------------------------------------
-- procedure Vref_set ( byte in Vref ) is
-- --------------------------------------------------------------------


-- --------------------------------------------------------------------
-- Comparator constants
-- --------------------------------------------------------------------
const byte _CMCOM_mode0 = 0  -- all comparator, but outputs reset (0)
const byte _CMCOM_mode1 = 1  -- this mode is not working, use mode 5 instead
const byte _CMCOM_mode2 = 2  -- A0,A1,A2,A4-Vref (4 comparators !!)
const byte _CMCOM_mode3 = 3  -- A2-A1 , A2-A0 = comparator , A3 = digital
const byte _CMCOM_mode4 = 4  -- A2-A1 , A0-A3 = comparator
const byte _CMCOM_mode5 = 5  -- A2-A1 = comparator    A0,A3 = digital
const byte _CMCOM_mode6 = 6  -- A2-A1 , A2-A0 = comparator,
                             -- A3 = output(A2-A0)
                             -- A4 = output(A2-A1) open drain !!
const byte _CMCOM_mode7 = 7  -- A0..A3 = digital , no comparator
-- --------------------------------------------------------------------



-- --------------------------------------------------------------------
-- global definitions
-- --------------------------------------------------------------------
var byte vrcon_shadow    -- contains settings with Vref=0 !!



-- --------------------------------------------------------------------
-- Initializes the Vref generator,
-- multiplexer = 0
--   high_low_range
--     = true,  Vout = Vdd * Vref / 24
--     = false, Vout = ( Vdd * Vref / 32 ) + ( Vdd / 4 )
--   output_enabled
--     = true,  pin_A2 (IC-pin 1) is set to input !!
--              if set to output, extra current comsumption and no output
--     = false, pin_A2 is left unchanged
--
-- The user is responsible for setting _CMCON correct
-- Pay special attention to the use of A2, because incresed current consumption
-- can occure if this pin is driven by different sources.
--
-- The setting of Vref generator is stored in the global vrcon_shadow
-- --------------------------------------------------------------------
procedure Vref_init (
  bit in high_low_range = true ,
  bit in output_enabled = false ) is

  -- if output enabled, set A2 as INPUT !!!
  -- otherwise do nothing
  if output_enabled then pin_a2_direction = input end if
  
  -- set VRCON, with the mux at the lowest value
  -- vrcon_shadow contains the settings without Vref !!
  vrcon_shadow = 0x80
  if high_low_range then vrcon_shadow = vrcon_shadow | 0x20 end if
  if output_enabled then vrcon_shadow = vrcon_shadow | 0x40 end if
  VRCON = vrcon_shadow
end procedure
-- --------------------------------------------------------------------



-- --------------------------------------------------------------------
-- set the Vref generator off,
-- by disabling Vref, disabling output,
-- pin A2 is not affected
-- --------------------------------------------------------------------
procedure Vref_off is
  VRCON = 0
end procedure
-- --------------------------------------------------------------------



-- --------------------------------------------------------------------
-- set the Vref multiplexer
-- all other Vref settings are not affected
-- --------------------------------------------------------------------
procedure Vref_set ( byte in Vref ) is
  VRCON = vrcon_shadow | ( Vref & 0x0F )
  -- don't know yet if this is usefull ???
  -- delay 10 us
end procedure
-- --------------------------------------------------------------------
